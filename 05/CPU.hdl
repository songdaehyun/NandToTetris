// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //RegisterA part
    Not(in=instruction[15] , out=NotAinstruction );
    Or(a=NotAinstruction , b=instruction[5] , out=LoadRA );
    ARegister(in=OutForRA , load=LoadRA , out=RAout, out[0..14]=addressM );//A
    Mux16(a=instruction , b=FBout , sel=instruction[15] , out=OutForRA );

    //RegisterD part
    And(a=instruction[15] , b=instruction[4] , out=LoadRD );
    DRegister(in=FBout , load=LoadRD , out=RDout);//D

    //inM Part
    Mux16(a=RAout , b=inM , sel=instruction[12] , out=outForALU );

    //ALU Part
    ALU(x=RDout , y=outForALU , zx=instruction[11] , nx=instruction[10] , zy=instruction[9] , ny=instruction[8] , f=instruction[7] , no=instruction[6] , out=FBout , out=outM,  zr=zr , ng=ng );

    //WirteM Part
    And(a=instruction[15] , b=instruction[3] , out=writeM );


    //For Program Counter Part
    Or(a=zr , b=ng , out=tmp1 );
    Not(in=tmp1 , out=ps );

    And(a=instruction[2] , b=ng , out=ngbit );
    And(a=instruction[1] , b=zr , out=zrbit );
    And(a=instruction[0] , b=ps , out=psbit );

    Or(a=ngbit , b=zrbit , out=tmp2 );
    Or(a=tmp2 , b=psbit , out=tmp3 );
    And(a=instruction[15] , b=tmp3 , out=PCload );

    PC(in=RAout , load=PCload , inc=true , reset=reset , out[0..14]=pc );


	//// Replace this comment with your code.
}